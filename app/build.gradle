plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "org.jetbrains.kotlin.kapt"
    id "dagger.hilt.android.plugin"
}

android {

    compileSdk 31

    defaultConfig {
        applicationId "ademar.tvmaze"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            keyAlias System.getenv("TV_MAZE_KEY_ALIAS")
            keyPassword System.getenv("TV_MAZE_KEY_PASSWORD")
            storeFile file("../assets/keystore")
            storePassword System.getenv("TV_MAZE_STORE_PASSWORD")
            v1SigningEnabled false
            v2SigningEnabled true
        }
    }

    buildTypes {
        all {
            buildConfigField "String", "TV_MAZE_ROOT_URL", "\"https://api.tvmaze.com/\""
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {

    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.2"))

    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.1"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.google.android.material:material:1.4.0"
    implementation "com.google.dagger:hilt-android:$dagger_version"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.okhttp3:logging-interceptor"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "io.reactivex.rxjava3:rxjava:3.1.1"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.7"

    testImplementation "junit:junit:4.13.2"

}
